{-# STDLIB_VERSION 3 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let NONE = "none"
let FEATURED = "featured"
let DAPP = ""
let LISTINGFEE = (1 * 100000000)
let WHALE = "whale"
let WHITELISTED = "registered"
func getKeyItemData(item: String) = {
    "datajson_" + item
}

func getStrByKey(key: String) = {
    let str = match getString(this, key) {
            case a:String => a
            case _ => NONE
    }
    str
}

func getNumberByKey(key: String) = {
    let num = match getInteger(this, key) {
            case a:Int => a
            case _ => 0
    }
    num
}

# Task author section
func getKeyItemAuthor (item: String) = {
    "author_" + item
}

func getValueItemAuthor(item: String) = {
    getStrByKey(getKeyItemAuthor(item))
}

func getKeyItemBlock(item: String) = {
    "block_" + item
}
func getKeyItemBank(item: String) = {
    "bank_" + item
}
func getKeyItemStatus(item: String) = {
    "status_" + item
}

func getKeyItemExpiration(item: String) = {
    "expiration_block_" + item
}

func getKeyWhitelistBio(account: String) = {
    "wl_bio_" + account
}

func getKeyWhitelistStatus(account: String) = {
    "wl_sts_" + account
}

func getValueWhitelistStatus(account: String) = {
    getStrByKey(getKeyWhitelistStatus(account))
}

func getKeyWhitelistBlock(account: String) = {
    "wl_blk_" + account
}

@Callable(i)
func createTask(item: String, expiration: Int, data: String) = {
    let account = toBase58String(i.caller.bytes)
    let pmt = extract(i.payment)
    if (isDefined(pmt.assetId)) then throw("can use waves only at the moment")
    else {
        if (pmt.amount != LISTINGFEE)
            then throw("Please pay exact amount for the listing: " + toString(LISTINGFEE) + ", actual payment is: " + toString(pmt.amount))
        else if (getValueItemAuthor(item) != NONE)
            then throw("Item already exist")
        else WriteSet([
            DataEntry(getKeyItemAuthor(item), account),
            DataEntry(getKeyItemBlock(item), height),
            DataEntry(getKeyItemExpiration(item), height + expiration),
            DataEntry(getKeyItemBank(item), LISTINGFEE),
            DataEntry(getKeyItemStatus(item), FEATURED),
            DataEntry(getKeyItemData(item), data)
        ])
    }
}

@Callable(i)
func signUp(data: String, type: String) = {
    let account = toBase58String(i.caller.bytes)
    WriteSet([
        DataEntry(getKeyWhitelistBio(account), data),
        DataEntry(getKeyWhitelistBlock(account), height),
        DataEntry(getKeyWhitelistStatus(account), if (type == WHALE) then WHALE else WHITELISTED)
    ])
}